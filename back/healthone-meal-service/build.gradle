plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	// QueryDSL plugins append
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
	id "org.sonarqube" version "3.0"
}

group = 'com.secui'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// starter-web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// Configuring the Annotation Processor
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// MariaDB
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	// QueryDSL
	implementation 'com.querydsl:querydsl-core:5.0.0'
	implementation "com.querydsl:querydsl-jpa:5.0.0"
	implementation "com.querydsl:querydsl-apt:5.0.0"
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0"
	// trace sql parameter lib
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.6.1'
	// OpenApi 3.0
	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.14'
	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok' // 테스트 의존성 추가
	testAnnotationProcessor 'org.projectlombok:lombok' // 테스트 의존성 추가
	// mapstruct
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0' // v1.18.16+ 부터
	implementation 'org.mapstruct:mapstruct:1.4.2.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

	implementation 'com.google.guava:guava:31.1-jre'
	// validation check
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// API 테스트를 위한 의존성
	testImplementation 'io.rest-assured:rest-assured:4.4.0'
	// json mapping
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

	//JWT
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
	implementation 'com.opencsv:opencsv:5.5.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

// QueryDSL setting
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}
configurations {
	querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}